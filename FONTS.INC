' -- GENERAL FONTS ROUTINE --

' PRINT SMALL FONT 8x8
SUB PrintS(X,Y,TP,TX$)
 LOCAL XSCR,YSCR,YY,T,TT,I
 SELECT CASE TP
  CASE 0: XSCR= 0: YSCR= 32 'GOLDEN
  CASE 1: XSCR= 0: YSCR=124 'BLUE
 END SELECT
 FOR T=1 TO LEN(TX$)
  YY=YSCR
  I=PEEK(VAR TX$,T)
  TT=XSCR+(I-33)*8-(I>=46)*16
  IF TT>312 THEN INC TT,-320: INC YY,8
  IF TT>312 THEN INC TT,-320: INC YY,8
  BLIT TT,YY,X,Y,8,8,37,4
  INC X,8
 NEXT T
END SUB

' PRINT FAST SMALL FONT 8x8 - ONLY NUMBERS
SUB PrintFS(X,Y,TP,TX$)
 LOCAL XSCR,YSCR,T,W,I
 W=32+92*(TP=1) 
 FOR T=1 TO LEN(TX$)
  I=PEEK(VAR TX$,T)
  BLIT 104+(I-48)*8,W,X,Y,8,8,37,4
  INC X,8
 NEXT T
END SUB

' PRINT NARROW FONT 8x16
SUB PrintN(X,Y,TP,TX$)
 LOCAL XSCR,YSCR,YY,T,TT,I
 SELECT CASE TP
  CASE 0: XSCR= 0: YSCR= 0 'GOLDEN
  CASE 1: XSCR= 0: YSCR=92 'BLUE
 END SELECT
 FOR T=1 TO LEN(TX$)
  YY=YSCR
  I=PEEK(VAR TX$,T)
  TT=XSCR+(I-65)*8-(I>=97)*40
  IF I>=33 AND I<=59 THEN TT=104+(I-33)*8: INC YY,16
  IF TT>312 THEN INC TT,-320: INC YY,16
  IF TT>312 THEN INC TT,-320: INC YY,16
  BLIT TT,YY,X,Y,8,16,37,4
  INC X,8
 NEXT T
END SUB

' PRINT MEGA FONT 16x16 / 8x19
SUB PrintM(X,Y,TP,TX$)
 LOCAL XSCR,YSCR,YY,T,TT,SZ,I
 SELECT CASE TP
  CASE 0: XSCR= 0: YSCR= 57 'GOLDEN
  CASE 1: XSCR= 0: YSCR=149 'BLUE
 END SELECT
 FOR T=1 TO LEN(TX$)
  YY=YSCR: SZ=16
  I=PEEK(VAR TX$,T)
  TT=XSCR+(I-65)*16
  IF I>=48 AND I<=57 THEN TT=160*(TP=0)+(I-48)*16: YY=184
  IF I>=97 THEN
   TT=96+(I-97)*8+(I>=109)*8+(I>=119)*8: INC YY,16
   'FIX M AND W (SIZE=16)
   IF I<>109 AND I<>119 THEN SZ=8 ELSE INC TT,-8
  ENDIF
  IF TT>312 THEN INC TT,-320: INC YY,16
  IF TT>312 THEN INC TT,-320: INC YY,16
  BLIT TT,YY,X,Y,SZ,16+(SZ=8)*3,37,4
  INC X,SZ
 NEXT T
END SUB

' PRINT FAST MEGA FONT - ONLY NUMBERS
SUB PrintFM(X,Y,TP,TX$)
 LOCAL XSCR,YSCR,T,W,I
 W=160*(TP=0)
 FOR T=1 TO LEN(TX$)
  I=PEEK(VAR TX$,T)
  BLIT W+(I-48)*16,184,X,Y,16,16,37,4
  INC X,16
 NEXT T
END SUB



