'MODE 7,16
'PAUSE(1000)

DIM ANI(30)

SUB LoadIntro
 FOR T=1 TO 18
  PAGE RESIZE T,320,240 'FIX RESOLUTION TO THE MAIN GAME INTRO
 NEXT T
 PAGE WRITE  0: CLS RGB(0,0,0)
 PAGE WRITE  2: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/MAPRED01.PNG"
 PAGE WRITE  3: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/MAPRED02.PNG"
 PAGE WRITE  4: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/MAPRED03.PNG"
 PAGE WRITE  5: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/MAPRED04.PNG"
 PAGE WRITE  6: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/MAPRED05.PNG"
 PAGE WRITE  7: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/00_MAP.PNG"
 PAGE WRITE  8: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/01_MAP_TEXT.PNG"
 PAGE WRITE  9: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/02_HAGGAR_DAMND_TEXT.PNG"
 PAGE WRITE 10: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/03_HAGGAR_PHONE.PNG"
 PAGE WRITE 11: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/04_HAGGAR_DAMND_TV.PNG"
 PAGE WRITE 12: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/05_JESSICA_DAMND_TV.PNG"
 PAGE WRITE 13: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/06_CODY_GUY_RING.PNG"
 PAGE WRITE 14: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/07_DAMND_CODY_GUY_TEXT.PNG"
 PAGE WRITE 15: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/TITLE.PNG"
 PAGE WRITE 16: CLS RGB(0,0,0)
END SUB

SUB GameIntro
 LOCAL I,T,XT,TT,TIM,YTXT,YSCR,PTXT,PART,EX

 PLAY STOP: EX=0

 T=KEYDOWN(1)
 IF T=27 OR T=32 THEN PLAY STOP: EX=1
 
 FOR T=2 TO 6
  ANI(T-2)=T
 NEXT T
 FOR T=2 TO 4
  ANI(T+3)=7-T
 NEXT T

 IF EX=0 THEN
  PAGE COPY 7 TO 1,B
  FadeIn
  PAGE WRITE 0
 ENDIF

 YTXT=0: YSCR=0: PTXT=1: PART=0: RD=0

 TIMER=0
 
 'FOR TEST ONLY
 'PART=3:YTXT=160:PTXT=2:SP=-2:TIM=255

 'FIRST TEXT WITH MAP FLASHING
 DO
  T=KEYDOWN(1)
  IF T=27 OR T=32 OR EX=1 THEN PLAY STOP: EX=1: EXIT DO
  INC TIM
  IF TIM>10 THEN TIM=0: INC I,0.5: IF I>7 THEN I=0
  BLIT 0,0,26,61,253,100,INT(ANI(I)),4
  T=170
  IF PTXT>0 THEN INC PTXT,-1
  IF PTXT=5 THEN TT=PIXEL(MM.HRES-1,MM.VRES-1):BOX 0,T,MM.HRES,MM.VRES-T,,TT,TT
  'START MUSIC INTRO
  IF PTXT=1 AND PART=3 THEN PLAY MP3 "./MUSIC/Intro + SFX.mp3"
  IF PTXT=1 AND PART=4 THEN EXIT DO
  IF PTXT=0 THEN ReadLine(8,4,YTXT,T+YSCR)
  IF RD<0 AND PTXT=0 THEN   
   INC YTXT,16: INC YSCR,16
   IF YSCR>32+(PART=2)*16 THEN
    YSCR=0: PTXT=300: INC PART
   ENDIF
  ENDIF
  PAUSE(3.5)
 LOOP

 IF EX=0 THEN

  'SOMETIME IN THE 1990'S 
  FadeOut
  PAGE WRITE 1
  CLS 0
  BLIT 0,208,63,105,195,13,8,4
  FadeInFull
  PAUSE 2400
  FadeOut

  'HAGGAR ANSWERS THE PHONE
  PAGE WRITE 1
  CLS 0
  BLIT 0,0,0,30,320,112,10
  FadeIn
 ENDIF

 'HAGGAR START TO TALK WITH DAMND
 YTXT=0: YSCR=0: PTXT=1: PART=0: RD=30: XT=0
 DO
  T=KEYDOWN(1)
  IF T=27 OR T=32 OR EX=1 THEN PLAY STOP: EX=1: EXIT DO
  T=146
  IF PTXT>0 THEN INC PTXT,-1
  IF PTXT=5 THEN TT=PIXEL(MM.HRES-1,MM.VRES-1):BOX 0,T,MM.HRES,MM.VRES-T,,TT,TT
  'IF PTXT=1 AND PART=6 THEN EXIT DO
  IF TIMER>42500 THEN EXIT DO
  'SCROLL ANGRY HAGGAR
  IF PART=2 AND XT<=64 AND RD>32 THEN
   IF XT<64 THEN BLIT 224,0,224+INT(XT),30,64-INT(XT),112,10
   BLIT 96-INT(XT),113,32,30,192+INT(XT),112,10
   INC XT,0.5
  ENDIF
  IF PTXT=0 THEN ReadLine(9,-8,YTXT,T+YSCR)
  IF RD<0 AND PTXT=0 THEN   
   INC YTXT,16: INC YSCR,24: RD=30
   IF YSCR>48-(PART=0)*48-(PART>=4)*24 THEN
    YSCR=0: PTXT=250: INC PART: IF PART=6 THEN PTXT=9999
   ENDIF
  ENDIF
  PAUSE(3.75)
 LOOP

 'HAGGAR TURN ON THE TV
 IF EX=0 THEN
  BLIT 2,0,34,30,256,112,11
  PAUSE(150)
  FOR T=1 TO 15
   BLIT 258,0,90,59,62,51,11
   PAUSE(30)
   BLIT 258,53,90,58,62,52,11 
   PAUSE(30)
  NEXT T
  FadeOut

  'FADE IN JESSICA ON THE TV
  PAGE WRITE 1: CLS 0
  BLIT 0,0,32,30,320,112,12
  FadeIn
 ENDIF

 'SHOWS THE IMAGE FROM THE TV
 YTXT=0: YSCR=0: PTXT=1: PART=0: RD=30: XT=0
 DO
  T=KEYDOWN(1)
  IF T=27 OR T=32 OR EX=1 THEN PLAY STOP: EX=1: EXIT DO
  INC XT
  II=(COS(TIMER/25)*1+1)*((TIMER>56200) AND (TIMER<57700)) 'SHAKE DAMND WHEN LAUGHING

  IF PART<2 THEN
   ' JESSICA TV
   IF XT=10 THEN BLIT 0,  0,32,30,320,112,12
   IF XT=20 THEN BLIT 0,113,96,46,128, 80,12:XT=0
  ELSE
   ' DAMND TV
   'IF XT=10 THEN BLIT 129,113,96,46,128,80,12
   'IF XT=20 THEN BLIT 0,113,96,46,128,80,11: XT=0

   IF XT<=10 THEN
    BLIT 129,113,96,46+II,128,80-II,12
   ELSE
    BLIT   0,113,96,46+II,128,80-II,11
    IF XT>=20 THEN XT=0
   ENDIF

  ENDIF
  T=146
  IF PTXT>0 THEN INC PTXT,-1
  IF PTXT=5 THEN TT=PIXEL(MM.HRES-1,MM.VRES-1):BOX 0,T,MM.HRES,MM.VRES-T,,TT,TT
  IF PTXT=9500 AND PART=3 THEN TT=PIXEL(MM.HRES-1,MM.VRES-1):BOX 0,T,MM.HRES,MM.VRES-T,,TT,TT
  'WAIT TO THE DAMND LAUGH AND WAIT TO KEEP THE DIFFERENCES BETWEEN 400 TO 507 MHZ
  IF TIMER>59000 THEN EXIT DO 
  IF PTXT=0 THEN ReadLine(14,-8,YTXT,T+YSCR)
  IF RD<0 AND PTXT=0 THEN   
   INC YTXT,16: INC YSCR,24: RD=30
   IF YSCR>48-(PART=0)*24 THEN
    YSCR=0: PTXT=350: INC PART    
    IF PART=3 THEN PTXT=9999
   ENDIF
  ENDIF
  PAUSE(3.75)

 LOOP

 IF EX=0 THEN
  FadeOut
  PAUSE(450)

  'START SHOWING THE RING
  PAGE WRITE 17
  BLIT  0,128,0,30,172,112,13
  BLIT 70,128,154,30,102,112,13
  BLIT 70,128,218,30,102,112,13

  PAGE WRITE 1
  CLS 0
  BLIT  0,128,34,30,172,112,13
  BLIT 70,128,188,30,102,112,13
  FadeIn
 ENDIF

 YTXT=128: YSCR=0: PTXT=0: PART=0: RD=30: XT=0
 DO
  T=KEYDOWN(1)
  IF T=27 OR T=32 OR EX=1 THEN PLAY STOP: EX=1: EXIT DO
  T=146
  IF XT<=64 THEN
   PAGE WRITE 1
   BLIT XT+34,30,32,30,256,112,17 'RING
   BLIT 70,128,254-XT,30,102,112,13
   BLIT 172,128,256-XT*1.8,30,148,112,13,4 'GUY
   BLIT 114,0,256-XT*3.95,30,206,112,13,4 'CODY
   PAGE WRITE 0
   BLIT 34,30,34,30,256,112,1
   INC XT,0.5
  ENDIF
  IF PTXT>0 THEN INC PTXT,-1
  IF PTXT=5 THEN TT=PIXEL(MM.HRES-1,MM.VRES-1):BOX 0,T,MM.HRES,MM.VRES-T,,TT,TT
  IF PTXT=1 AND PART=4 THEN EXIT DO
  IF PTXT=0 THEN ReadLine(14,-4,YTXT,T+YSCR)
  IF RD<0 AND PTXT=0 THEN   
   INC YTXT,16: INC YSCR,24
   IF YSCR>(PART=3)*24 THEN
    YSCR=0: PTXT=300: INC PART: RD=30
   ENDIF
  ENDIF
  PAUSE(3.8)

 LOOP
 
 IF EX=0 THEN
  FadeOut
  PAUSE 200
 ENDIF

END SUB

' SHOW TITLE
SUB GameTitle

 PAGE WRITE 0
 CLS 0
 
 'TITLE (FORCE 8-BIT MODE ON THE CMM2 G1 - TOO MANY VIDEO PAGES FOR IT)
 IF CMG2=0 THEN
  MODE 7,8 
  PAGE WRITE 15: CLS RGB(0,0,0): LOAD PNG "./IMAGES/INTRO/TITLE.PNG"  
 ENDIF
 PAGE COPY 15 TO 1,I
 PAGE SCROLL 1,2,0
 FadeIn
 
END SUB

' READ IMAGES WITH TEXT
SUB ReadLine(PG,XTXT,YR,YW)
 BLIT RD,YR,RD+XTXT,YW,1,16,PG,4
 PAGE WRITE PG
 INC RD
 IF PIXEL(RD,YR+2)=&HFFFF00 THEN RD=-1
 PAGE WRITE 0
END SUB

' FADEOUT
SUB FadeOut
 LOCAL T
 FOR T=255 TO 0 STEP -15

  IF VMODE<>400 THEN
   PAGE COPY 0 TO 16,I,0
  ELSE
   PAGE WRITE 16
   BLIT 0,0,0,0,384,240,0
  ENDIF

  PAGE WRITE 17: CLS RGB(T,T,T)
  PAGE AND_PIXELS 16,17,17

  IF VMODE<>400 THEN
   PAGE COPY 17 TO 0,B,0
  ELSE
   PAGE WRITE 0
   BLIT 0,0,0,0,384,240,17
  ENDIF

 NEXT T
 PAGE WRITE 0
END SUB

' FADEINFULL
SUB FadeInFull
 LOCAL T
 FOR T=0 TO 255 STEP 15

  IF VMODE<>400 THEN
   PAGE COPY 1 TO 16,I,0
  ELSE
   PAGE WRITE 16
   BLIT 0,0,0,0,384,240,1
  ENDIF

  PAGE WRITE 17: CLS RGB(T,T,T)
  PAGE AND_PIXELS 16,17,17

  IF VMODE<>400 THEN
   PAGE COPY 17 TO 0,B,0
  ELSE
   PAGE WRITE 0
   BLIT 0,0,0,0,384,240,17
  ENDIF

 NEXT T
 PAGE COPY 1 TO 0,B
 PAGE WRITE 0
END SUB

' FADEIN
SUB FadeIn
 LOCAL T
 FOR T=150 TO 255 STEP 5

  IF VMODE<>400 THEN
   PAGE COPY 1 TO 16,I,0
  ELSE
   PAGE WRITE 16
   BLIT 0,0,0,0,384,240,1
  ENDIF

  PAGE WRITE 17: CLS RGB(T,T,T)
  PAGE AND_PIXELS 16,17,17

  IF VMODE<>400 THEN
   PAGE COPY 17 TO 0,B,0
  ELSE
   PAGE WRITE 0
   BLIT 0,0,0,0,384,240,17
  ENDIF

 NEXT T
 PAGE COPY 1 TO 0,B
 PAGE WRITE 0
END SUB

