'CALL START MENU AND OPTIONS MENU

SUB GameMenu

 PLAY STOP
 PLAY MODFILE ".\SFX\SFX - MENU.MOD",22050
 PLAY_SAMPLE 2,1

 YOP=212

 ' WAIT TO NOT JUMP THE MENUS
 DO WHILE KEYDOWN(1)=32: LOOP

 DO ' -- MENU --------------------------------------------------------

  PAGE WRITE 1

  BOX 0,165,320,60,,0,0

  PrintS(110,179,1-(YOP=212),"GAME START")
  PrintS(110,199,1-(YOP=232),"OPTION MENU") 

  PutObject(90,YOP,15,4,1)

  PAGE COPY 1 TO 0,B

  FOR T=1 TO 6
   KB=ASC(UCASE$(CHR$(KEYDOWN(T))))
   SELECT CASE KB 
    CASE K_U, K_D: IF GO=1 THEN PLAY_SAMPLE 1,1: GO=0: IF YOP=212 THEN YOP=232 ELSE YOP=212
    CASE 32: EXIT DO
    CASE 27: YOP=0: EXIT DO
   END SELECT
  NEXT T

  KB=ASC(UCASE$(CHR$(KEYDOWN(1))))
  IF KB<>K_U AND KB<>K_D THEN GO=1

 LOOP
 
 ' WAIT TO NOT JUMP THE MENUS
 DO WHILE KEYDOWN(1)=32: LOOP

 IF YOP=232 THEN 'OPTION MENU
  FadeOut
  I=0
  YOP=0

  'CONFIGURE RESOLUTION OPTION
  TT=(VMODE=384)+(VMODE=400)*2
 
  DO ' -- OPTION MENU -------------------------------------------------

   PAGE WRITE 1
   CLS RGB(0,0,0)
   PrintM(70,10,0,"OPTION MENU")
   
   II=60: T=0
   PrintS(32,II+T*16,1-(YOP=T),"1P CONTROLLER:"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"2P CONTROLLER:"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"GAME LEVEL ```"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"LIVES      ```"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"EXTRA LIFE ```"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"BGM        ```"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"RESOLUTION ```"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"COLOUR     ```"):INC T
   PrintS(32,II+T*16,1-(YOP=T),"EXIT          "):INC T

   IF CMG2 THEN
    PrintN(36,224,0,"CMM2 G2 - ENHANCED MODES ENABLED")
   ELSE
    PrintN(32,224,0,"CMM2 G1 - ENHANCED MODES DISABLED")
   ENDIF

   PutObject(14,93+YOP*16,15,4,1)

   II=60
   'CONTROLLER TYPE
   FOR T=0 TO 1
    IF TCType(T+1)<0 THEN TCType(T+1)=2
    IF TCType(T+1)>2 THEN TCType(T+1)=0
    IF CtrlExist(1)=0 AND CtrlExist(2)=0 THEN TCType(T+1)=0
    IF CtrlExist(1)>0 OR CtrlExist(2)>0 OR T=0 THEN
     SELECT CASE TCType(T+1)
      CASE 0: PrintS(152,II+T*16,1-(YOP=T),"KEYB` SHIFT CTRL ALT") 
      CASE 1: IF CtrlExist(1)>0 THEN PrintS(152,II+T*16,1-(YOP=T),"NES/SNES CLASSIC MINI") ELSE TCType(T+1)=0
      CASE 2: IF CtrlExist(2)>0 THEN PrintS(152,II+T*16,1-(YOP=T),"WII CLASSIC") ELSE TCType(T+1)=0
     END SELECT
    ELSE
     PrintS(152,II+T*16,1-(YOP=T),"NONE DETECTED") 
    ENDIF
    CtrlType(T+1)=TCType(T+1)
   NEXT T

   'GAME LEVEL
   IF GLEVEL<0 THEN GLEVEL=3
   IF GLEVEL>3 THEN GLEVEL=0
   SELECT CASE GLEVEL
    CASE 0: PrintS(152,II+T*16,1-(YOP=T),"EASY")
    CASE 1: PrintS(152,II+T*16,1-(YOP=T),"NORMAL")
    CASE 2: PrintS(152,II+T*16,1-(YOP=T),"HARD")
    CASE 3: PrintS(152,II+T*16,1-(YOP=T),"MANIA")
   END SELECT
   INC T

   'LIVES
   IF LIVES<0 THEN LIVES=9
   IF LIVES>9 THEN LIVES=0
   PrintS(152,II+T*16,1-(YOP=T),STR$(LIVES))
   INC T

   'EXTRA LIFE
   IF EXLIFE<0 THEN EXLIFE=3
   IF EXLIFE>3 THEN EXLIFE=0
   SELECT CASE EXLIFE
    CASE 0: PrintS(152,II+T*16,1-(YOP=T),"100000 pts")
    CASE 1: PrintS(152,II+T*16,1-(YOP=T),"200000 pts")
    CASE 2: PrintS(152,II+T*16,1-(YOP=T),"100000, EVERY 200000 pts")
    CASE 3: PrintS(152,II+T*16,1-(YOP=T),"NONE")
   END SELECT
   INC T

   'BGM
   IF BGM THEN A$="ON" ELSE A$="OFF"
   PrintS(152,II+T*16,1-(YOP=T),A$)
   INC T

   'GAME LEVEL
   IF TT<0 THEN TT=2
   IF TT>2 THEN TT=0
   SELECT CASE TT
    CASE 0: VMODE=320:PrintS(152,II+T*16,1-(YOP=T),"320x240")
    CASE 1: VMODE=384:PrintS(152,II+T*16,1-(YOP=T),"384x240")
    CASE 2: VMODE=400:PrintS(152,II+T*16,1-(YOP=T),"400x300"): DEPTH=8
   END SELECT
   INC T

   'COLOUR DEPTH
   SELECT CASE DEPTH
    CASE  8: PrintS(152,II+T*16,1-(YOP=T),"8-BIT (RECOMMENDED)")
    CASE 16: PrintS(152,II+T*16,1-(YOP=T),"16-BIT (SLOWER)")
   END SELECT
   INC T

   IF I=0 THEN FadeIN: I=1

   PAGE COPY 1 TO 0,B

   FOR T=1 TO 6
    KB=ASC(UCASE$(CHR$(KEYDOWN(T))))
    SELECT CASE KB 
     CASE K_U: IF GO=1 THEN PLAY_SAMPLE 1,1: GO=0: INC YOP,-1
     CASE K_D: IF GO=1 THEN PLAY_SAMPLE 1,1: GO=0: INC YOP
     CASE 27: YOP=0: EXIT DO
     CASE 32: IF YOP=8 THEN YOP=0: EXIT DO
     CASE K_L,K_R:
      IF GO=1 THEN
       PLAY_SAMPLE 1,1
       GO=0
       SELECT CASE YOP
        'CONTROLLER
        CASE 0,1: INC TCType(YOP+1),(KB=K_R)-(KB=K_L)
        'GAME LEVEL
        CASE   2: INC GLEVEL,(KB=K_R)-(KB=K_L)
        'LIVES
        CASE   3: INC LIVES,(KB=K_R)-(KB=K_L)
        'EXTRA LIFE
        CASE   4: INC EXLIFE,(KB=K_R)-(KB=K_L)
        'BGM
        CASE   5: IF BGM=1 THEN BGM=0 ELSE BGM=1
        'RESOLUTION
        CASE   6: INC TT,(KB=K_R)-(KB=K_L)
        'COLOUR DEPTH
        CASE   7: IF DEPTH=8 THEN DEPTH=16 ELSE DEPTH=8
       END SELECT
      ENDIF
    END SELECT
   NEXT T

   'OPTION LIMIT
   IF YOP>8 THEN YOP=0
   IF YOP<0 THEN YOP=8

   'DON'T ALLOW KEYBOARD RUNS CRAZY
   KB=ASC(UCASE$(CHR$(KEYDOWN(1))))
   IF KB<>K_U AND KB<>K_D AND KB<>K_L AND KB<>K_R THEN
    GO=1
    IF CMG2=0 THEN DEPTH=8: TT=0
   ENDIF

  LOOP

  FadeOut
  CLS: PAUSE 100
  YOP=0

 ELSE 'CONTINUE TO SELECT PLAYER AND START GAME
  PLAY_SAMPLE 4,4,,100
  PAUSE 600: FadeOut
  CLS: PAUSE 100

  IF CMG2 THEN MODE 7,16 ELSE MODE 7,8
  SelectPlayer
  YOP=1
 ENDIF

END SUB
